roboname := "TheMachine"

robot initialization(){
	num turnDirection := 1
	bool moving := false
	num remainingDistance := 0
	
	setBodyColor(Color.gray())
	setGunColor(Color.lightGray())
	setRadarColor(Color.red())
	setBulletColor(Color.black())
}

robot behavior(){
	if (moving){
		if (remainingDistance > 0){
			ahead(remainingDistance)
			moving := false
		}else{
			remainingDistance -:= 20
			ahead(20)
			scan()
		}
	}else{
		turnGunRight(1440 * turnDirection)
	}
}

event onScannedRobot(ScannedRobotEvent e){
	if (moving){
		CleverFire(e.getDistance())
	}else{
		moving := true
		if (e.getHeading() - getHeading() >=0){
			turnDirection := 1
		}else{
			turnDirection := -1
		}

		num difference := getGunHeading() - getHeading()
		if (difference > 180){
			turnGunRight(360 - difference)
		}else{
			turnGunLeft(difference)
		}
		
		turnRight(e.getBearing())
		remainingDistance := e.getDistance() + 5
		if (remainingDistance > 200){
			remainingDistance := 200
		}
		scan()
	}
}


func void CleverFire(num distance){
	if (distance < 50){
		fire(3)
	}else if (distance < 100){
		fire(2)
	}else if (distance < 200){
		fire(1)
	}
}

event onHitRobot(HitRobotEvent e){
	if (e.getBearing() >= 0){
		turnDirection := 1
	}else{
		turnDirection := -1
	}
	turnRight(e.getBearing())
	
	fire(3)
	
	//turnRight(e.getBearing()) := 2
	// Determine a shot that won't kill the robot...
	// We want to ram him instead for bonus points
	if (e.getEnergy() > 16){
		fire(3)
	}else if (e.getEnergy() > 10){
		fire(2)
	}else if (e.getEnergy() > 4){
		fire(1)
	}else if (e.getEnergy() > 2){
		fire(0.5)
	}else if (e.getEnergy() > 0.4){
		fire(0.1)
	}
	ahead(40) // Ram him again!
}